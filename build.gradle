buildscript {
    repositories { jcenter() }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.2',
            // asciidoc gradle plugin
            // http://asciidoctor.org/docs/asciidoctor-gradle-plugin/
            'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
    }
}

apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'
// asciidoc - for documentaton
// sourceDir	
// 		where the asciidoc sources are. Type: File. Default: src/asciidoc.
//
// sourceDocumentNames	
// 		an override to process multiple source files, which may be a subset 
//		of all files available in ${sourceDir}. Type: FileCollection. 
//		Defaults to all files in ${sourceDir}.
apply plugin: 'org.asciidoctor.convert'
apply plugin: 'application'

def vertxVersion = '3.2.0'
version = '1.0.0'

sourceCompatibility = 1.8 
targetCompatibility = 1.8 

if (!JavaVersion.current().java8Compatible) {
    throw new IllegalStateException("This needs Java 8. You are using ${JavaVersion.current()}.")
}

repositories {
    mavenCentral()
    maven { url 'http://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'http://www.systap.com/maven/releases' }
    maven { url 'http://nexus.bedatadriven.com/content/groups/public' }
    jcenter()
    mavenLocal()
    flatDir {
        dirs 'lib'
    }
}

dependencies {
    compile 'io.vertx:vertx-core:' + vertxVersion
    compile 'io.vertx:vertx-web:' + vertxVersion
    compile 'io.vertx:vertx-codetrans:' + vertxVersion
    compile 'io.vertx:vertx-auth:' + vertxVersion
    compile 'io.vertx:vertx-hazelcast:' + vertxVersion
    compile 'io.vertx:vertx-lang-groovy:' + vertxVersion
    compile 'io.vertx:vertx-web-templ-thymeleaf:' + vertxVersion
    compile 'com.github.jknack:handlebars:1.3.0'
    compile 'org.thymeleaf:thymeleaf:2.1.4.RELEASE'
    compile fileTree(dir: 'lib', include: '*.jar')
    compile 'com.bigdata:bigdata:1.5.3'
    compile 'xyz.bpdp.vertx:blazegraph-service:1.0.0'
    testCompile 'junit:junit:4.12'
}

shadowJar {
    classifier = 'fat'
    manifest {
        attributes 'Main-Class': 'xyz.bpdp.vastix.core.VastixServer'
    }
    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.9'
}

mainClassName = 'xyz.bpdp.vastix.core.VastixServer'
